
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.scoverage:gradle-scoverage:1.0.9'
    }
}

plugins {
    id 'scala'
    id 'com.github.maiflai.scalatest' version '0.9'
    id 'org.jetbrains.intellij' version '0.0.26'
}
apply plugin: 'scoverage'
apply plugin: 'org.jetbrains.intellij'

description = """<p>Language support for <a href="http://fitnesse.org>">FitNesse</a>, the fully integrated standalone wiki and acceptance testing framework.</p>

<p>This plugin provides syntax highlighting, auto-completion and execution of FitNesse test suites all from within your IDE.</p>
"""

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

ext {
    FITNESSE_VERSION = '20151230'
    SCALA_VERSION = '2.11.7'
}

def jdkHome = System.getenv("JAVA_HOME")

dependencies {
    compile "org.scala-lang:scala-library:${SCALA_VERSION}"
    compile("org.fitnesse:fitnesse:${FITNESSE_VERSION}:standalone") { transitive = false }
    compile project('idea-fitnesse_rt')

    testCompile 'org.scalatest:scalatest_2.11:2.2.5'
    testCompile 'org.mockito:mockito-core:1.10.19'

    // Scalatest requires:
    testRuntime 'org.pegdown:pegdown:1.1.0'
    testRuntime files("$jdkHome/lib/tools.jar")

    // task testScoverage
    scoverage "org.scala-lang:scala-library:${SCALA_VERSION}"
    scoverage 'org.scoverage:scalac-scoverage-plugin_2.11:1.1.1'
    scoverage 'org.scoverage:scalac-scoverage-runtime_2.11:1.1.1'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

processResources {
    from('src/main/resources') {
        include 'META-INF/plugin.xml'
        filter(ReplaceTokens, tokens: [
                version : project.version,
                description: project.description ])
    }
}

test {
    // Avoid parallel execution, since the IntelliJ boilerplate is not up to that
    maxParallelForks = 1
}

task dist(type: Zip, dependsOn: [jar, test]) {
    from configurations.compile
    from jar.archivePath
    rename { f -> "lib/${f}" }
    into 'idea-fitnesse'
    baseName 'idea-fitnesse'
}

intellij {
    version '15.0.5'
    pluginName 'idea-fitnesse'

    publish {
        username jetbrainsUsername ?: ""
        password jetbrainsPassword ?: ""
        pluginId '7908'
    }
}

task coverage {
    dependsOn 'testScoverage'
    dependsOn 'reportScoverage'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
