
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'scala'
    id 'com.github.maiflai.scalatest' version '0.9'
    id 'org.jetbrains.intellij' version '0.0.42'
    id 'org.scoverage' version '2.0.1'
}

description = """<p>Language support for <a href="http://fitnesse.org>">FitNesse</a>, the fully integrated standalone wiki and acceptance testing framework.</p>

<p>This plugin provides syntax highlighting, auto-completion and execution of FitNesse test suites all from within your IDE.</p>
"""

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

ext {
    FITNESSE_VERSION = '20151230'
    SCALA_VERSION = '2.11.7'
}

dependencies {
    compile "org.scala-lang:scala-library:${SCALA_VERSION}"
    compile("org.fitnesse:fitnesse:${FITNESSE_VERSION}:standalone") { transitive = false }
    compile project('idea-fitnesse_rt')

    testCompile 'org.scalatest:scalatest_2.11:2.2.5'
    testCompile 'org.mockito:mockito-core:1.10.19'

    // Scalatest requires:
    testRuntime 'org.pegdown:pegdown:1.1.0'

    // task testScoverage
    scoverage "org.scala-lang:scala-library:${SCALA_VERSION}"
    scoverage 'org.scoverage:scalac-scoverage-plugin_2.11:1.1.1'
    scoverage 'org.scoverage:scalac-scoverage-runtime_2.11:1.1.1'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

test {
    // Avoid parallel execution, since the IntelliJ boilerplate is not up to that
    maxParallelForks = 1
}

intellij {
    version ideaVersion //'2016.1' //'15.0.5'
    pluginName 'idea-fitnesse'

    publish {
        username jetbrainsUsername ?: ""
        password jetbrainsPassword ?: ""
        pluginId '7908'
    }
}

task coverage {
    dependsOn 'testScoverage'
    dependsOn 'reportScoverage'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

// vim:sw=4:et:syntax=groovy
